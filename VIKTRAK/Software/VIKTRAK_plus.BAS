1 POKE36879,239
2 PRINT"{CLR}{DOWN}{BLACK} VIKTRAK+ 1.3 BY K7NH":REM COPYRIGHT 9/1/84
3 PRINT"{DOWN}{DOWN}    DISTRIBUTED BY","{DOWN}SPECTRUM WEST, SEATTLE"
4 PRINTTAB(8)"{DOWN}{DOWN}MENU":PRINTTAB(5)"{DOWN}A{BLUE}=TRACKING":PRINTTAB(5)"{BLACK}B{BLUE}=SAT DATA
6 PRINTTAB(5)"{BLACK}C{BLUE}=FREQ INFO":PRINTTAB(5)"{BLACK}D{BLUE}=AUTOTRAK":PRINTTAB(5)"{BLACK}E{BLUE}=SAVE PGM"
7 PRINTTAB(4)"{DOWN}{DOWN}{DOWN}{BLACK}SELECT CHOICE{BLUE}"
8 GETM$:IFM$=""GOTO8
10 IFM$="A"GOTO18
12 IFM$="B"GOTO600
14 IFM$="C"THENGOSUB950:GOTO2
15 IFM$="D"GOTO1000
16 IFM$="E"THENPRINT:SAVE"VIKTRAK+ 1.3":GOTO2
17 GOTO8
18 PRINT"{CLR}{BLACK}{DOWN}  NECESSARY PGM INFO{BLUE}":GOSUB70:GOSUB600
20 A=INT(1440/PE):B=1440-(PE*A):C=(LI*A)-360
22 IFDY<DRTHENDY=DY+365
24 E=DY-DR:F=MR-(B*E):G=LR+(C*E)
26 IFF<PETHENF=F+PE:G=G+LI:GOTO26
28 IFF>=PETHENF=F-PE:G=G-LI:GOTO28
30 IFG>360THENG=G-360
32 GOSUB96:GOTO100
40 A=3600-(14400*(EC>.1))
42 S%=S/-16.36+16:IFS>260THENS%=S%+22
43 U%=7988-INT((U+50)/10)*22:S%=S%+U%
44 POKES%,96:POKES%+30720,1:FORL=1TO9:GETA$:ON-(A$<>"")GOTO54:NEXT
45 POKES%,224:ON-(A$="P")GOTO91:FORL=1TO60:ON-(A<=TI)GOTO64:NEXT:GOTO44
54 IFA$="A"THENPRINT"{BLACK}{DOWN}CALC AOS, {R}A{_}=RETURN
56 IFA$="M"GOTO2
58 IFA$="N"THENGOSUB74:GOSUB600:GOTO20
60 IFA$="P"THENOPEN1,4:PRINT#1,CHR$(13)"SAT  DATE        TIME";:GOTO90
62 IFA$="T"THENGOSUB96:GOTO160
64 TI$="000000":T=T+TC:M=M+TC:IFM>=60THENM=M-60:H=H+1
66 IFH>=24THENH=H-24:DY=DY+1:GOSUB82
68 GOTO160
70 PRINT"{Q}ENTER DATE,EX 01JAN86
72 INPUT"P";D$:PRINT"{_}":IFLEN(D$)<>7GOTO42
74 D=VAL(LEFT$(D$,2)):LY=VAL(RIGHT$(D$,2)):IFD>31ORD<1GOTO72
76 GOSUB86:FORA=1TO12:IFMID$(D$,3,3)<>MID$(M$,3*A,3)THENNEXT:GOTO72
78 GOSUB88:DY=VAL(MID$(M$,3*A,3)):LD=0
80 LY=LY/100:LY=INT(100*(LY-INT(LY))+.1):IFLY/4=INT(LY/4)ANDDY>=59THENLD=1
81 DY=DY+LD+D
82 IFDY>365+LDTHENDY=DY-365
84 GOSUB88:FORA=12TO1STEP-1:D=VAL(MID$(M$,3*A,3))+LD:IFDY<=DTHENNEXT
85 D=DY-D:GOSUB86:DM$=MID$(M$,3*A,3):RETURN
86 M$="XXJANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC":RETURN
88 M$="XX000031059090120151181212243273304334":RETURN
90 PRINT#1,"     BNG  EL"CHR$(16)"42PHASE"CHR$(16)"50LON"CHR$(16)"57LAT"
91 PRINT#1,N$" "D;DM$CHR$(16)"18"H$":"M$;
92 IFEL<=90THENPRINT#1,CHR$(16)"26"INT(BG)CHR$(16)"33"INT(EL);
93 PRINT#1,CHR$(16)"42"PA CHR$(16)"49"INT(S)CHR$(16)"56"INT(U)
94 IFEC>.1THENTC=30:IFPA<40ORPA>215THENTC=10
95 GOTO64
96 PRINT"{Q}INPUT UTC TIME,EX:0250
97 INPUT"{P}";U$:PRINT"{<-}":IFLEN(U$)<>4GOTO97
98 H=VAL(LEFT$(U$,2)):M=VAL(RIGHT$(U$,2)):IFH>23ORM>59GOTO97
99 T=(H*60+M)-F:PRINT"P     CALCULATING":TI$="000000":RETURN
100 REM ORBIT CALC
110 DEF FNI(X)=ATN(X/SQR(-X*X+1))
120 DEF FNO(X)=-ATN(X/SQR(-X*X+1))+PI/2
130 T=.001:R=PI/180:K=57.296
140 PH=PE/256
150 HT=47.8:HN=122.4:REM SEATTLE,WA
160 E=(360/PE)*T
170 A=E+(K*EC)*SIN(E*R)*(1+EC*(COS(E*R)))
180 FORL=1TO4:C=A-(K*EC)*(SIN(A*R)):B=E-C
190 U=B/(1-EC*COS(A*R)):A=A+U:NEXTL
200 IFA>=360THENA=A-360
210 V=2*ATN(SQR((1+EC)/(1-EC))*TAN((A*R)/2))/R:IFA>180THENV=V+360
220 AL=SE*(1-EC*COS(A*R))-6371
230 C=W+V:IFC>=360THENC=C-360
240 U=FNI(SIN(I*R)*SIN(C*R))/R
250 L=360-(ATN(COS(I*R)*TAN(C*R))/R)+.25*T
260 IFL>=360THENL=L-360
270 IFCOS(C*R)<0THENL=L-180:IFL<0THENL=L+360
280 IFT=.001THENN=L:S=G
290 S=S-(N-L):N=L:IFS>=360THENS=S-360
300 IFS<0THENS=S+360
310 IFT=.001THENT=(H*60+M)-F:GOTO160
320 L=HN-S:IFL>180THENL=L-360
330 IFL<=-180THENL=L+360
340 C=FNO(SIN(HT*R)*SIN(U*R)+COS(HT*R)*COS(U*R)*COS(L*R))
350 BG=FNO((SIN(U*R)-SIN(HT*R)*COS(C))/(COS(HT*R)*SIN(C)))/R
360 IFL<0THENBG=360-BG
370 EL=90-ATN((AL+6371)*SIN(C)/((AL+6371)*COS(C)-6371))/R
375 GETB$:IFB$="A"ORB$="P"THENA$="":CLOSE1,4
380 TC=1-(4*(EC>.1)):IFEL<=90GOTO420
390 IFA$<>"A"GOTO500
400 IFEL<175THENTC=TC*2:IFEL<163THENTC=TC*5
410 PRINT"{F}";:GOTO64
420 REM AUTOROTOR
430 A=(180+BG)*.71:IFA>255THENA=A-255
440 POKE39936,A
450 B=EL*1.4:POKE38912,B
500 PA=INT(T/PH):IFPA<0THENPA=PA+256
505 IFPA>255THENPA=PA-256:GOTO505
510 H$=RIGHT$(STR$(INT(H+100)),2):M$=RIGHT$(STR$(INT(M+100)),2):GOSUB900
520 PRINT"{Q}SAT:{P}"N$" {_}DATE:{P}"D;DM$
530 PRINT"{_}{Q}TIME"TAB(6)"BNG"TAB(10)"EL"TAB(14)"LON"TAB(18)"LAT
540 PRINT"{P}"H$":"M$;:IFEL<=90THENPRINTINT(BG)INT(EL);
550 PRINTTAB(13)INT(S)INT(U)"{_}":IFEC>.1THENPRINT"PHASE{P}"PA"{_}
560 GOTO40
600 IFM$="B"THENPRINT"{S}{Q}ENTER {P}RUN{_} TO CONTINUE
610 PRINT"{Q}ENTER NAMEOF SAT","{R}O9,O10,O11{R}, RRS5R-R8R, RSTSR "
620 INPUT"P";N$:PRINT"{_}"
650 ON-(N$<>"RS5")GOTO660:IFM$="B"THENLIST652-656
652 W=0:I=82.96:SE=8034:EC=0
654 PE=119.554:LI=30.02
656 DR=346:MR=16.4:LR=236.4
658 RETURN
660 ON-(N$<>"RS6")GOTO670:IFM$="B"THENLIST662-666
662 W=0:I=82.96:SE=7996:EC=0
664 PE=118.72:LI=29.8
666 DR=23:MR=39.9:LR=118.6
668 RETURN
670 ON-(N$<>"RS7")GOTO680:IFM$="B"THENLIST672-676
672 W=0:I=82.96:SE=8017:EC=0
674 PE=119.194:LI=29.925
676 DR=346:MR=37.60:LR=247.4
678 RETURN
680 ON-(N$<>"RS8")GOTO690:IFM$="B"THENLIST682-686
682 W=0:I=82.96:SE=8043:EC=0
684 PE=119.762:LI=30.067
686 DR=346:MR=90.9:LR=252.0
688 RETURN
690 ON-(N$<>"STS")GOTO710:IFM$="B"THENLIST692-696
692 W=0:I=45.5:SE=6762:EC=0
694 PE=92.15:LI=23.44
696 DR=1:MR=60:LR=100
698 RETURN

710 ON-(N$<>"O9")GOTO720:IFM$="B"THENLIST712-716
712 W=0:I=97.635:SE=6864:EC=0
714 PE=94.329:LI=23.58
716 DR=346:MR=56.4:LR=116.76
718 RETURN
720 ON-(N$<>"O11")GOTO800:IFM$="B"THEN LIST722-726
722 W=0:I=98.19:SE=7062.4:EC=0
724 PE=98.554:LI=24.638
726 DR=314:MR=59.2:LR=45.2
728 RETURN
800 ON-(N$<>"O10")GOTO620:IFM$="B"THENLIST802-806
802 I=26.35:SE=26106:EC=.597
804 PE=699.45:LI=175.42
806 DR=334:MR=582:LR=225:W=65
808 GOTO890
890 MR=MR-PE/2:LR=LR+(360-LI)/2:IFMR<0THENMR=MR+PE:LR=LR+LI
892 IFLR>=360THENLR=LR-360
894 RETURN
900 PRINT"{S}{P}A{_}=AOS,{P}N{_}=NEW SAT,{P}M{_}=MENU{P} T{_}=TIME {P}P{_}=PRINTER OUT

// LINES 910 TO 934 WORLD MAP IS PRINTED

934 RETURN
950 PRINT"{S}{P} SATELLITE FREQ INFO"
952 PRINT"{Q}{P}OSCAR 10"
954 PRINT"B{_}-TRANS BCN..145.810"
956 PRINT"UP 435.025>435.175 LSB";
958 PRINT"DN 145.025<145.828 USB";
960 PRINT"{P}L{_}-TRANS BCN..436.040"
962 PRINT"UP 1268.05>1268.85 LSB";
964 PRINT"DN  436.96<436.15 USB";
966 PRINT"{Q}{P}OSCAR 9, 11{_}  145.825
970 PRINT"{Q}{P}RS5/RS6{_} BCN...29.400"
972 PRINT"UP 145.910>145.950 USB";
974 PRINT"DN  29.410>29.450 USB";
976 PRINT"{P}RS7/RS8{_} BCN...29.500"
978 PRINT"UP 145.960>146.000 USB";
980 PRINT"DN  29.460>29.500 USB";
986 PRINT"{Q} PRESS {P}RETURN{_} TO CONT";
988 GETM$:IFM$=""GOTO988
990 RETURN
1000 REM AUTOTRAK
1010 PRINT"{S}{P}{Q} MANUALAUTOTRAK MODE
1020 PRINT"{Q}{_} ENTER RPB{_}RNG (0 TO 359)"
1030 PRINT"   EX:B45,B120, B345"
1040 PRINT"{Q} OR RPE{_}RLEV (0 TO 90)"
1050 PRINT"   EX:E0, E30, E65"
1060 PRINT"{Q}{P}   M{_}=RETURN TO PMENU{_}Q"
1070 INPUT"   ";AD$:AD=VAL(MID$(AD$,2)):IFLEFT$(AD$,1)="E"GOTO1130
1080 IF LEFT$(AD$,1)="M"GOTO1
1090 IFLEFT$(AD$,1)<>"B"THENPRINT" PRECEED BY B OR E?":GOTO1070
1100 IFAD>=360 THEN PRINT"BNG 0 TO 359?":GOTO1070
1110 AB=(180+AD)*.71:IFAB>255THENAB=AB-255
1120 PRINT" OK":POKE39936,AB:GOTO1070
1130 IFAD>90THENPRINT"ELEV 0 TO 90?:GOTO1070
1140 AE=AD*1.4:PRINT"OK":POKE38912,AE:GOTO1070
